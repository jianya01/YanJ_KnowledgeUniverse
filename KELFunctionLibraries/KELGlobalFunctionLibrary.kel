//-----------------------------
// KEL Global Function Library
//-----------------------------
/*********
* Notes *
*********/
// To use the functions in this library, simply add 
// IMPORT KELFunctionLibraries.KELGlobalFunctionLibrary; to your relevant KEL file.
// You can then simply call the refrence name.
// I.e. Person: => Age := AGE(dateofbirth);

// Functions in this library may not contain refrences in their definition to other
// local funcitons.



/*************************
* Arithematic Functions *
*************************/
// SmartDIV: 
// Returns the nullVal if dividing by zero. Currently no way to force null result in KEL.
FUNCTION: SmartDIV(REAL numerator, REAL denominator, REAL nullVal) <= IF(denominator=0,nullVal ,numerator/denominator);
// SmartDiv can be used in the predicate of an attribute to force devision by zero to result in NULL, as so:
// Business: SmartDIV(booksales,totalsales,-9999) <> -9999 => percentsalesbooks := booksales / totalsales;
// ...Or you could skip the function and do: 
// Business: totalsales <> 0 => percentsalesbooks := booksales / totalsales;



/***************************
* Date and Time Functions *
***************************/
// LessThanXDaysOld: 
// Returns boolean value whether the input date is less days old than the input number of days.
FUNCTION: LessThanXDaysOld(DATE dateCheck, INTEGER days) <= IF(/*DAYSBETWEEN(dateCheck,CURRENTDATE()) >= 0 AND*/ DAYSBETWEEN(dateCheck,CURRENTDATE()) <= days, TRUE, FALSE);

// SmartDAYSBETWEEN: 
// Returns the numbe rof days between the from date and the to date, order does not matter.
FUNCTION: SmartDAYSBETWEEN(DATE from, DATE to) <= ABS(DAYSBETWEEN(from,to));

// SmartYEARSBETWEEN: 
// Returns the number of years between the from date and the to date, order does not matter.
FUNCTION: SmartYEARSBETWEEN(DATE from, DATE to) <= ABS(YEARSBETWEEN(from,to));

// AGEDays:
// Returns the number of days between the input date and the current date.
FUNCTION: AGEDays(DATE indate) <= ABS(DAYSBETWEEN(indate,CURRENTDATE()));

// AGE:
// Returns the number of years between the input date and the current date.
FUNCTION: AGE(DATE indate) <= ABS(YEARSBETWEEN(indate,CURRENTDATE()));



/**********************
* Rounding Functions *
**********************/
//NOTE: KEL may still ignore your rounding and tack on 000..001 to your value.

 // RoundPercent: 
 // Takes an input ratio and turns it into a percentage, rounding to the specified number of decimal places.
FUNCTION: RoundPercent(REAL ratio, INTEGER numDecimalPlaces) <= ROUND(ratio * POWER(10,numDecimalPlaces+2)) / POWER(10,numDecimalPlaces);

// RoundTo: 
// Takes and input REAL value and rounds to the specified number of decimal places. 
FUNCTION: RoundTo(REAL inReal, INTEGER numDecimalPlaces) <= ROUND(inReal * POWER(10,numDecimalPlaces)) / POWER(10,numDecimalPlaces);
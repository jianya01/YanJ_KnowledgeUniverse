/*
This macro uses the history file generated by the internal linking process to expand the match candidates
to construct a new set of match candidates that can answer the question: 'what would be fetched at any point
in time'.
Parameters:
	- infile: the file to be patched
  - in_did_field
	- in_rid_field
	- in_dt_early
	- in_dt_late
	- co_collapses: the history file (record: old_did | rid | new_did | date of collapse)
	- co_old_did
	- co_new_did
	- co_rid
	- co_date
Steps:
 1) First project all records such that dt_first_usable is the in_dt_early and the dt_last_usable is blank. 
(For any dids that never collapsed - this is all we need)
 2) Join the output of 1 to the collapses file by rid and did = co_new_did - left outer (this tells you that 
this was when this did appeared). Set the in_dt_early to the co_date (in the case of a match). NO NEW RECORDS 
in this step.
 3) Now repeat the join performed in 2 but this time inner. In the transform change the in_did_field to 
the co_old_did and set the dt_last_usable to the co_date. This generates NEW records.
 4) We now need to effectively repeat step 2 using the output of step 3. This will clip the dt_first_usable 
of the parent records to when they were merged into
 5) We now repeat step 3 using the output of step 4
 6) Steps 4&5 LOOP until nothing appears from step 5
The output of the macro is now step 2 + Step 4 + Step 6 ++++
*/
EXPORT MAC_ASOF_Match_Candidates(infile, in_did_field, in_rid_field, in_dt_early, in_dt_late, co_collapses, co_old_did, co_new_did, co_rid, co_date) := FUNCTIONMACRO
  NewCandidatesRec := RECORD
		infile; 
		TYPEOF(infile.in_dt_early) dt_first_usable;
		TYPEOF(infile.in_dt_early) dt_last_usable;
		UNSIGNED2 level;
  END;
	NewCandidatesRec init_usables(infile le) := TRANSFORM
	  SELF.dt_first_usable := le.in_dt_early;
		SELF.dt_last_usable := (TYPEOF(le.in_dt_late))'';
		SELF.level := 1;
		SELF := le;
	END;
	infile1 := PROJECT(infile,init_usables(LEFT));
	loopBody(DATASET( NewCandidatesRec) ds, UNSIGNED2 Iteration) := FUNCTION	
	   ds1 := JOIN(ds,co_collapses,LEFT.in_rid_field=RIGHT.co_rid AND LEFT.in_did_field = RIGHT.co_new_did,
							TRANSFORM(NewCandidatesRec,
								SELF.dt_first_usable:=IF(RIGHT.co_date<>(TYPEOF(RIGHT.co_date))'',RIGHT.co_date,LEFT.dt_first_usable),
								SELF.level:=IF(RIGHT.co_date<>(TYPEOF(RIGHT.co_date))'',Iteration,LEFT.level),
								SELF:=LEFT;)
						,LEFT OUTER);
	   patch := JOIN(ds1(level=Iteration),co_collapses,LEFT.in_rid_field=RIGHT.co_rid AND LEFT.in_did_field = RIGHT.co_new_did,
								TRANSFORM(NewCandidatesRec,
									SELF.level:=Iteration+1,
									SELF.in_did_field:=RIGHT.co_old_did,
									SELF.dt_last_usable:=RIGHT.co_date,SELF:=LEFT;));
		
		RETURN ds1+patch;
  END;
	RETURN LOOP(infile1, LEFT.level=COUNTER, loopBody(ROWS(LEFT),COUNTER));
ENDMACRO;

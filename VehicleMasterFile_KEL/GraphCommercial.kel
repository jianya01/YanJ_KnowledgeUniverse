#OPTION(persist, 0) // Set to 0 to disable PERSIST functionality
// #OPTION(querytypes, 'normal') // Only generate the Q_* Functions for execution on Thor against the base files
// #OPTION(exportpreentity) // Force the Pre-Entity code generation so that the Thor code generated is optimized

	// --- ENTITY Definitions Section --- 
IMPORT KnowledgeUniverse.VehicleMasterFile_KEL.Entities;

// --- USE Statements Section ---
IMPORT VehicleMasterFileCom_KEL.Uses;

// --- Attributes Section ---
IMPORT KnowledgeUniverse.VehicleMasterFile_KEL.Attributes;


QUERY: VehicleMasterFileCommercial(PERMITS DPM) <= 
	Policy{
		VIN := VehiclePolicy.Vehicle.VIN,
		ValidVIN := VehiclePolicy.Vehicle.ValidVIN,
		VehiclePolicy(Vehicle.VIN != '' AND NOT Vehicle.VIN:Null).VehicleGroupID,
		VehicleGroupStart := VehiclePolicy.VehicleGroupStart,
		VehicleGroupEnd := VehiclePolicy.VehicleGroupEnd,
		LexID := PersonPolicy.Subject.UID,
		PrimaryPolicyHolderLexID := VehiclePolicy.G_LatestPrimaryHolder,
		LexIDStart := PersonPolicy.G_BestHolderStartDate, 
		LexIDEnd := PersonPolicy.G_BestHolderEndDate,
		BestCoverageStartDate := VehiclePolicy.G_BestCoverageStartDate,
		BestCoverageEndDate := VehiclePolicy.G_BestCoverageEndDate,
		NextGroupStart := VehiclePolicy.G_NextGroupStart,
		InvalidPolicy := VehiclePolicy.G_InvalidPolicy
	}
		((LexIDStart < BestCoverageEndDate) AND
		(LexIDEnd > BestCoverageStartDate OR NOT ISVALIDDATE(LexIDEnd) OR LexIDEnd:Null) AND
		NOT InvalidPolicy)
	{
		VIN,
		ValidVIN,
		CommercialVIN := TRUE,
		VehicleGroupID,
		VehicleGroupStart := MIN(GROUP,VehicleGroupStart),
		VehicleGroupEnd := IF(MAX(GROUP,VehicleGroupEnd) <= CURRENTDATE(), MAX(GROUP,VehicleGroupEnd),Policy.NULLDATEVALUE),
		LexID,
		LexIDStart := MAX(MIN(GROUP,LexIDStart),MIN(GROUP,VehicleGroupStart)),
		LexIDEnd := MAP(NOT MIN(GROUP,NextGroupStart):Null => MIN(MAX(GROUP,LexIDEnd), MAX(GROUP,BestCoverageEndDate), MIN(GROUP,NextGroupStart)),
						MIN(MAX(GROUP,LexIDEnd),MAX(GROUP,BestCoverageEndDate)) > CURRENTDATE() => Policy.NULLDATEVALUE,
						MIN(MAX(GROUP,LexIDEnd),MAX(GROUP,BestCoverageEndDate))),
		PrimaryFlag := MAX(GROUP,IF(LexID = PrimaryPolicyHolderLexID, 1, 0)) = 1
	}
		((LexIDStart < VehicleGroupEnd OR NOT ISVALIDDATE(VehicleGroupEnd) OR VehicleGroupEnd:Null OR VehicleGroupEnd = Policy.NULLDATEVALUE) AND
		(LexIDEnd > VehicleGroupStart OR NOT ISVALIDDATE(LexIDEnd) OR LexIDEnd:Null))
	{
		VIN,
		ValidVIN,
		CommercialVIN,
		VehicleGroupID,
		VehicleGroupStart,
		VehicleGroupEnd,
		LexID := 0,
		LexIDStart := 0,
		LexIDEnd := 0,
		PrimaryFlag := FALSE
	},
	Policy{
		VehiclePolicy(Vehicle.VIN != '' AND NOT Vehicle.VIN:Null).VehicleGroupID,
		LexID := PersonPolicy.Subject.UID,
		LexIDStart := PersonPolicy.G_BestHolderStartDate, 
		LexIDEnd := PersonPolicy.G_BestHolderEndDate,
		BestCoverageStartDate := VehiclePolicy.G_BestCoverageStartDate,
		BestCoverageEndDate := VehiclePolicy.G_BestCoverageEndDate,
		InvalidPolicy := VehiclePolicy.G_InvalidPolicy
	}
		((LexIDStart < BestCoverageEndDate) AND
		(LexIDEnd > BestCoverageStartDate OR NOT ISVALIDDATE(LexIDEnd) OR LexIDEnd:Null) AND
		NOT InvalidPolicy)
	{	
		LexID := 0,
		VehicleGroupID
	}
USING DPM;


